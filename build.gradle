plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '3.0.4'
    id 'nu.studer.jooq' version '7.1.1'
    id "org.sonarqube" version "3.5.0.2730"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'

group = 'me.renedo.shopping'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone/" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    //logging
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'com.vlkan.log4j2:log4j2-logstash-layout:1.0.5'
    //Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:4.2.1'
    //Database drivers
    implementation('org.springframework.boot:spring-boot-starter-jooq') {
        //The spring implementation has a very old versiÃ³n of jooq,
        exclude group: 'org.jooq', module: 'jooq'
    }
    if (project.hasProperty("mariadb")) {
        implementation 'org.mariadb.jdbc:mariadb-java-client'
    } else {
        implementation 'com.h2database:h2:1.4.200'
    }
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.jooq:jooq:3.17.14'
    //ocr
    implementation 'net.sourceforge.tess4j:tess4j:5.4.0'// https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-textract
    implementation 'software.amazon.awssdk:textract:2.20.26'

    //tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    //jooq code generation
    jooqGenerator('org.jooq:jooq-meta-extensions:3.15.11') {
        //jooq generator has problems with h2 >=2 .* so we need to downgrade the h2 to the last 1.4 version
        exclude group: 'com.h2database', module: 'h2'
    }
    jooqGenerator 'com.h2database:h2:1.4.200'
}

test {
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

import org.jooq.meta.jaxb.Logging

jooq {
    version = '3.15.11'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        main {
            generationTool {
                logging = Logging.WARN
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }
                    database {
                        name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                        properties {
                            property {
                                key = 'scripts'
                                value = 'src/main/resources/scripts/create.sql'
                            }

                        }
                    }
                    generate {
                        relations = true
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'me.renedo.shopping.shared.jooq'
                    }
                }
            }
        }
    }
}

bootJar {
    launchScript()
}
